#include "toggle.h90"

MODULE dynzad
   !!======================================================================
   !!                       ***  MODULE  dynzad  ***
   !! Ocean dynamics : vertical advection trend
   !!======================================================================
   !! History :  OPA  ! 1991-01  (G. Madec) Original code
   !!   NEMO     0.5  ! 2002-07  (G. Madec) Free form, F90
   !!----------------------------------------------------------------------
   
   !!----------------------------------------------------------------------
   !!   dyn_zad       : vertical advection momentum trend
   !!----------------------------------------------------------------------
   USE oce            ! ocean dynamics and tracers
   USE dom_oce        ! ocean space and time domain
   USE sbc_oce        ! surface boundary condition: ocean
   USE trd_oce        ! trends: ocean variables
   USE trddyn         ! trend manager: dynamics
   !
   USE in_out_manager ! I/O manager
   USE lib_mpp        ! MPP library
   USE prtctl         ! Print control
   USE timing         ! Timing
   use mpi

   IMPLICIT NONE
   PRIVATE
   
   PUBLIC   dyn_zad       ! routine called by dynadv.F90

   !! * Substitutions
#  include "vectopt_loop_substitute.h90"
   !!----------------------------------------------------------------------
   !! NEMO/OCE 4.0 , NEMO Consortium (2018)
   !! $Id: dynzad.F90 10068 2018-08-28 14:09:04Z nicolasmartin $
   !! Software governed by the CeCILL license (see ./LICENSE)
   !!----------------------------------------------------------------------
CONTAINS

   SUBROUTINE dyn_zad ( kt )
      !!----------------------------------------------------------------------
      !!                  ***  ROUTINE dynzad  ***
      !! 
      !! ** Purpose :   Compute the now vertical momentum advection trend and 
      !!      add it to the general trend of momentum equation.
      !!
      !! ** Method  :   The now vertical advection of momentum is given by:
      !!         w dz(u) = ua + 1/(e1e2u*e3u) mk+1[ mi(e1e2t*wn) dk(un) ]
      !!         w dz(v) = va + 1/(e1e2v*e3v) mk+1[ mj(e1e2t*wn) dk(vn) ]
      !!      Add this trend to the general trend (ua,va):
      !!         (ua,va) = (ua,va) + w dz(u,v)
      !!
      !! ** Action  : - Update (ua,va) with the vert. momentum adv. trends
      !!              - Send the trends to trddyn for diagnostics (l_trddyn=T)
      !!----------------------------------------------------------------------
      INTEGER, INTENT(in) ::   kt   ! ocean time-step inedx
      !
      INTEGER  ::   ji, jj, jk   ! dummy loop indices
      REAL(wp) ::   zua, zva     ! local scalars
      REAL(wp) ::   zwuwp2, zwuwm1, zwvwp2, zwvwm1     ! local scalars
      REAL(wp), DIMENSION(jpi,jpj)     ::   zww
      REAL(wp), DIMENSION(jpi,jpj,jpk) ::   zwuw, zwvw
      REAL(wp), DIMENSION(:,:,:), ALLOCATABLE ::   ztrdu, ztrdv
      !!----------------------------------------------------------------------
      !
# if defined dynzad_time
       external :: cyc_time
       integer*8 :: st, ed, elapsed 
       real(wp) :: qbw
       integer :: print_flag
       common /flag/ print_flag
# endif
# if defined slave
      integer, external :: slave_dynzad
      type :: dynzad_var
         integer   :: jpi, jpj, jpk, fs_two, fs_jpim1, jpjm1, jpkm1, narea
         integer*8 :: loc_un, loc_vn, loc_wn, loc_e1e2t, loc_zwuw, loc_zwvw, loc_r1e1e2u, loc_r1e1e2v, loc_e3un, loc_e3vn, loc_ua, loc_va
      end type dynzad_var
      type(dynzad_var) :: var_z
      var_z%jpi = jpi
      var_z%jpj = jpj
      var_z%jpk = jpk
      var_z%fs_two = fs_2
      var_z%fs_jpim1 = fs_jpim1
      var_z%jpjm1 = jpjm1
      var_z%jpkm1 = jpkm1
      var_z%narea = narea
      var_z%loc_un = loc(un)
      var_z%loc_vn = loc(vn)
      var_z%loc_wn = loc(wn)
      var_z%loc_e1e2t = loc(e1e2t)
      var_z%loc_zwuw = loc(zwuw)
      var_z%loc_zwvw = loc(zwvw)
      var_z%loc_r1e1e2u = loc(r1_e1e2u)
      var_z%loc_r1e1e2v = loc(r1_e1e2v)
      var_z%loc_e3un = loc(e3u_n)
      var_z%loc_e3vn = loc(e3v_n)
      var_z%loc_ua = loc(ua)
      var_z%loc_va = loc(va)
# endif             

      IF( ln_timing )   CALL timing_start('dyn_zad')
      !
      IF( kt == nit000 ) THEN
         IF(lwp) WRITE(numout,*)
         IF(lwp) WRITE(numout,*) 'dyn_zad : 2nd order vertical advection scheme'
      ENDIF

# if defined dynzad_time
         if(lwp) call cyc_time(st)
# endif
# if defined slave
         call athread_spawn(slave_dynzad, var_z)
         DO jj = 2, jpjm1
            DO ji = fs_2, fs_jpim1
               zwuwp2 = 0.25_wp*(e1e2t(ji+1,jj)*wn(ji+1,jj,2) + e1e2t(ji,jj)*wn(ji,jj,2))*(un(ji,jj,1) - un(ji,jj,2)) 
               zwvwp2 = 0.25_wp*(e1e2t(ji,jj+1)*wn(ji,jj+1,2) + e1e2t(ji,jj)*wn(ji,jj,2))*(vn(ji,jj,1) - vn(ji,jj,2))
               zwuwm1 = 0.25_wp*(e1e2t(ji+1,jj)*wn(ji+1,jj,jpkm1) + e1e2t(ji,jj)*wn(ji,jj,jpkm1))*(un(ji,jj,jpkm1-1) - un(ji,jj,jpkm1)) 
               zwvwm1 = 0.25_wp*(e1e2t(ji,jj+1)*wn(ji,jj+1,jpkm1) + e1e2t(ji,jj)*wn(ji,jj,jpkm1))*(vn(ji,jj,jpkm1-1) - vn(ji,jj,jpkm1))
               ua(ji,jj,1) = ua(ji,jj,1) - zwuwp2*r1_e1e2u(ji,jj)/e3u_n(ji,jj,1)
               va(ji,jj,1) = va(ji,jj,1) - zwvwp2*r1_e1e2v(ji,jj)/e3v_n(ji,jj,1)
               ua(ji,jj,jpkm1) = ua(ji,jj,jpkm1) - zwuwm1*r1_e1e2u(ji,jj)/e3u_n(ji,jj,jpkm1)
               va(ji,jj,jpkm1) = va(ji,jj,jpkm1) - zwvwm1*r1_e1e2v(ji,jj)/e3v_n(ji,jj,jpkm1)
            END DO  
         END DO  
         call athread_join()
# else
      IF( l_trddyn )   THEN         ! Save ua and va trends
         ALLOCATE( ztrdu(jpi,jpj,jpk) , ztrdv(jpi,jpj,jpk) ) 
         ztrdu(:,:,:) = ua(:,:,:) 
         ztrdv(:,:,:) = va(:,:,:) 
      ENDIF
      DO jk = 2, jpkm1              ! Vertical momentum advection at level w and u- and v- vertical
         DO jj = 2, jpj                   ! vertical fluxes 
            DO ji = fs_2, jpi             ! vector opt.
               zww(ji,jj) = 0.25_wp * e1e2t(ji,jj) * wn(ji,jj,jk)
            END DO
         END DO
         DO jj = 2, jpjm1                 ! vertical momentum advection at w-point
            DO ji = fs_2, fs_jpim1        ! vector opt.
               zwuw(ji,jj,jk) = ( zww(ji+1,jj  ) + zww(ji,jj) ) * ( un(ji,jj,jk-1) - un(ji,jj,jk) )
               zwvw(ji,jj,jk) = ( zww(ji  ,jj+1) + zww(ji,jj) ) * ( vn(ji,jj,jk-1) - vn(ji,jj,jk) )
            END DO  
         END DO   
      END DO
      !
      ! Surface and bottom advective fluxes set to zero
      DO jj = 2, jpjm1        
         DO ji = fs_2, fs_jpim1           ! vector opt.
            zwuw(ji,jj, 1 ) = 0._wp
            zwvw(ji,jj, 1 ) = 0._wp
            zwuw(ji,jj,jpk) = 0._wp
            zwvw(ji,jj,jpk) = 0._wp
         END DO  
      END DO
      !
      DO jk = 1, jpkm1              ! Vertical momentum advection at u- and v-points
         DO jj = 2, jpjm1
            DO ji = fs_2, fs_jpim1       ! vector opt.
               ua(ji,jj,jk) = ua(ji,jj,jk) - ( zwuw(ji,jj,jk) + zwuw(ji,jj,jk+1) ) * r1_e1e2u(ji,jj) / e3u_n(ji,jj,jk)
               va(ji,jj,jk) = va(ji,jj,jk) - ( zwvw(ji,jj,jk) + zwvw(ji,jj,jk+1) ) * r1_e1e2v(ji,jj) / e3v_n(ji,jj,jk)
            END DO  
         END DO  
      END DO
      IF( l_trddyn ) THEN           ! save the vertical advection trends for diagnostic
         ztrdu(:,:,:) = ua(:,:,:) - ztrdu(:,:,:)
         ztrdv(:,:,:) = va(:,:,:) - ztrdv(:,:,:)
         CALL trd_dyn( ztrdu, ztrdv, jpdyn_zad, kt )
         DEALLOCATE( ztrdu, ztrdv ) 
      ENDIF
# endif
# if defined dynzad_time
         if(lwp) then
           call cyc_time(ed)
           elapsed = ed - st
           qbw = 8*2.25*(jpi*jpj*jpk*11+jpi*jpj*3)/((elapsed-60000)*45.0)
         end if
# endif
# if defined dynzad_time
         if(lwp .and. kt == 3) then
           print *, 'dynzad_time: ', elapsed, ' BW: ', qbw
         endif
# endif

      !                             ! Control print
      IF(ln_ctl)   CALL prt_ctl( tab3d_1=ua, clinfo1=' zad  - Ua: ', mask1=umask,   &
         &                       tab3d_2=va, clinfo2=       ' Va: ', mask2=vmask, clinfo3='dyn' )
      !
      IF( ln_timing )   CALL timing_stop('dyn_zad')
      !
   END SUBROUTINE dyn_zad

   !!======================================================================
END MODULE dynzad
